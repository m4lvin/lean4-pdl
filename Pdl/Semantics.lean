import Mathlib.Data.Finset.Basic
import Mathlib.Data.Set.Lattice
import Mathlib.Logic.Relation
import Mathlib.Order.CompleteLattice
import Mathlib.Order.FixedPoints

import Pdl.Syntax
import Pdl.Measures

-- Kripke Models aka Labelled Transition Systems
structure KripkeModel (W : Type) : Type where
  val : W → Char → Prop
  Rel : Char → W → W → Prop

@[simp]
def complexityOfQuery {W : Type} :
    PSum (Σ' (_ : KripkeModel W) (_ : W), Formula)
         (Σ' (_ : KripkeModel W) (_ : Program) (_ : W), W) → ℕ
  | PSum.inl val => lengthOfFormula val.snd.snd
  | PSum.inr val => lengthOfProgram val.snd.fst

mutual
  @[simp]
  def Evaluates {W : Type} : KripkeModel W → W → Formula → Prop
    | _, _, ⊥ => False
    | M, w, ·c => M.val w c
    | M, w, ~φ => Not (Evaluates M w φ)
    | M, w, φ⋀ψ => Evaluates M w φ ∧ Evaluates M w ψ
    | M, w, ⌈α⌉ φ => ∀ v : W, Relates M α w v → Evaluates M v φ
  @[simp]
  def Relates {W : Type} : KripkeModel W → Program → W → W → Prop
    | M, ·c, w, v => M.Rel c w v
    | M, α;'β, w, v => ∃ y, Relates M α w y ∧ Relates M β y v
    | M, α⋓β, w, v => Relates M α w v ∨ Relates M β w v
    | M, ∗α, w, v => Relation.ReflTransGen (Relates M α) w v
    | M, ?'φ, w, v => w = v ∧ Evaluates M w φ
end
termination_by
  Evaluates _ _ f => sizeOf f
  Relates _ p _ _ => sizeOf p

@[simp]
theorem evalDis {W M f g} {w : W} : Evaluates M w (f⋁g) ↔ Evaluates M w f ∨ Evaluates M w g :=
  by
  simp
  tauto

@[simp]
def evaluatePoint {W : Type} : KripkeModel W × W → Formula → Prop
  | (M, w), ϕ => Evaluates M w ϕ

def tautology (φ : Formula) :=
  ∀ (W : Type) (M : KripkeModel W) w, Evaluates M w φ

def contradiction (φ : Formula) :=
  ∀ (W : Type) (M : KripkeModel W) w, ¬Evaluates M w φ

-- MB: Definition 5, page 9
class HasSat (α : Type) where
  Satisfiable : α → Prop
open HasSat
@[simp]
instance formHasSat : HasSat Formula :=
  HasSat.mk fun ϕ => ∃ (W : _) (M : KripkeModel W) (w : _), Evaluates M w ϕ
@[simp]
instance setHasSat : HasSat (Finset Formula) :=
  HasSat.mk fun X => ∃ (W : _) (M : KripkeModel W) (w : _), ∀ φ ∈ X, Evaluates M w φ
@[simp]
instance listHasSat : HasSat (List Formula) :=
  HasSat.mk fun X => ∃ (W : _) (M : KripkeModel W) (w : _), ∀ φ ∈ X, Evaluates M w φ

def semImpliesSets (X : Finset Formula) (Y : Finset Formula) :=
  ∀ (W : Type) (M : KripkeModel W) (w),
    (∀ φ ∈ X, Evaluates M w φ) → ∀ ψ ∈ Y, Evaluates M w ψ

def semImpliesLists (X : List Formula) (Y : List Formula) :=
  ∀ (W : Type) (M : KripkeModel W) (w),
    (∀ φ ∈ X, Evaluates M w φ) → ∀ ψ ∈ Y, Evaluates M w ψ

def semEquiv (φ : Formula) (ψ : Formula) :=
  ∀ (W : Type) (M : KripkeModel W) (w), Evaluates M w φ ↔ Evaluates M w ψ

@[simp]
theorem singletonSat_iff_sat : ∀ φ, Satisfiable ([φ] : List Formula) ↔ Satisfiable φ :=
  by
  intro phi
  simp

theorem semEquiv.transitive : Transitive semEquiv :=
  by
  unfold Transitive
  unfold semEquiv
  intro f g h f_is_g g_is_h W M w
  specialize f_is_g W M w
  specialize g_is_h W M w
  tauto

class vDash (α : Type) (β : Type) where
  SemImplies : α → β → Prop

open vDash

instance modelCanSemImplyForm {W : Type} : vDash (KripkeModel W × W) Formula := vDash.mk (@evaluatePoint W)
instance modelCanSemImplySet {W : Type} : vDash (KripkeModel W × W) (Finset Formula) := vDash.mk (λ ⟨M,w⟩ fs => ∀ f ∈ fs, @Evaluates W M w f)
@[simp]
instance modelCanSemImplyList {W : Type} : vDash (KripkeModel W × W) (List Formula) := vDash.mk (λ ⟨M,w⟩ fs => ∀ f ∈ fs, @Evaluates W M w f)
instance setCanSemImplySet : vDash (Finset Formula) (Finset Formula) := vDash.mk semImpliesSets
instance setCanSemImplyForm : vDash (Finset Formula) Formula:= vDash.mk fun X ψ => semImpliesSets X {ψ}
instance formCanSemImplySet : vDash Formula (Finset Formula) := vDash.mk fun φ X => semImpliesSets {φ} X
instance formCanSemImplyForm : vDash Formula Formula := vDash.mk fun φ ψ => semImpliesSets {φ} {ψ}

infixl:40 "⊨" => SemImplies

infixl:40 "≡" => semEquiv

infixl:40 "⊭" => fun a b => ¬a⊨b

-- useful lemmas to connect different ⊨ cases
theorem forms_to_sets {φ ψ : Formula} : φ⊨ψ → ({φ} : Finset Formula)⊨({ψ} : Finset Formula) :=
  by
  intro impTaut
  intro W M w lhs ψ psi_in_psi
  specialize impTaut W M w
  simp at psi_in_psi lhs impTaut
  rw [psi_in_psi]
  -- needed even though no ψ_1 in goal here?!
  apply impTaut
  exact lhs

theorem notSat_iff_semImplies (X : Finset Formula) (χ : Formula):
    ¬Satisfiable (X ∪ {~χ}) ↔ X ⊨ ({χ} : Finset Formula) := by
  simp only [Satisfiable, Finset.mem_union, Finset.mem_singleton, not_exists, not_forall, exists_prop, setCanSemImplySet, semImpliesSets, forall_eq]
  constructor
  · intro nSat W M w satX
    specialize nSat W M w
    rcases nSat with ⟨φ, phi_in, not_phi⟩
    aesop
  · intro X_chi W M w
    specialize X_chi W M w
    -- cases em (evaluate M w χ)
    sorry

theorem relate_steps : ∀ x z, Relates M (Program.steps (as ++ bs)) x z  ↔
  ∃ y, Relates M (Program.steps as) x y ∧ Relates M (Program.steps bs) y z :=
  by
  induction as
  · simp
  case cons a as IH =>
    intro x z
    constructor
    · intro lhs
      simp at *
      rcases lhs with ⟨y, x_a_y, y_asbs_z⟩
      rw [IH] at y_asbs_z
      rcases y_asbs_z with ⟨y', y_as_ys', ys'_bs_z⟩
      use y'
      constructor
      · use y
      · exact ys'_bs_z
    · intro rhs
      simp at *
      rcases rhs with ⟨y, ⟨y', x_a_y', y'_as_y⟩, bla⟩
      use y'
      rw [IH y' z]
      tauto

-- TODO: remove this, special instance of previous?
theorem rel_steps_last {as} : ∀ v w,
  Relates M (Program.steps (as ++ [a])) v w ↔
    ∃ mid, Relates M (Program.steps as) v mid ∧ Relates M a mid w :=
  by
  induction as
  case nil =>
    simp at *
  case cons a2 as IH =>
    intro s t
    simp at *
    constructor
    · intro lhs
      rcases lhs with ⟨next, s_a2_next, next_asa_t⟩
      rw [IH] at next_asa_t
      tauto
    · intro rhs
      rcases rhs with ⟨m,⟨y,yP,yP2⟩,mP⟩
      use y
      rw [IH]
      tauto

theorem truthImply_then_satImply (X Y : Finset Formula) : X ⊨ Y → Satisfiable X → Satisfiable Y :=
  by
  intro X_Y
  intro satX
  rcases satX with ⟨W,M,w,v_X⟩
  specialize X_Y W M w v_X
  use W, M, w
