import Pdl.Flip
import Pdl.Soundness

/-! # Interpolants for Partitions (big part of Section 7)

Note that we can skip much of subsection 7.3 because we worked already with split tableaux anyway.
-/

open HasSat

/-! ## Joint vocabulary -/

/-- The joint vocabulary occurring on both the left and the right side. -/
@[simp]
def jvoc (X : Sequent) : Vocab := (X.left).fvoc ∩ (X.right).fvoc

lemma jvoc_sub_of_voc_sub {Y X : Sequent}
    (hl : Y.left.fvoc ⊆ X.left.fvoc)
    (hr : Y.right.fvoc ⊆ X.right.fvoc)
    : jvoc Y ⊆ jvoc X := by
  intro x x_in_jY
  simp only [jvoc, Finset.mem_inter] at x_in_jY
  specialize @hl x x_in_jY.1
  specialize @hr x x_in_jY.2
  simp only [jvoc, Finset.mem_inter]
  tauto

/-! ## Partition Interpolants -/

def isPartInterpolant (X : Sequent) (θ : Formula) :=
  θ.voc ⊆ jvoc X ∧ (¬ satisfiable ((~θ) :: X.left) ∧ ¬ satisfiable (θ :: X.right))

def PartInterpolant (N : Sequent) := Subtype <| isPartInterpolant N

/-- Like `Olf.voc` but without the ⊕ inside. -/
def onlfvoc : Option NegLoadFormula → Vocab
| none => ∅
| some nlf => NegLoadFormula.voc nlf

def lfovoc (L : List (List Formula × Option NegLoadFormula)) : Vocab :=
  L.toFinset.sup (fun ⟨fs,o⟩ => fs.fvoc ∪ (onlfvoc o))

/-! ## Interpolants for local rules -/

lemma LoadRule_voc (lr : LoadRule (~'χ) ress) : lfovoc ress ⊆ χ.voc := by
  intro x x_in
  unfold lfovoc at x_in
  simp at x_in
  rcases x_in with ⟨fs, onlf, in_ress, x_in_V⟩
  cases lr
  case dia α χ notAtom =>
    have unfvoc := @unfoldDiamond_voc x α χ.unload
    rw [← unfoldDiamondLoaded_eq α χ] at unfvoc
    specialize @unfvoc (pairUnload (fs, onlf)) (by simp only [List.mem_map]; use (fs,onlf))
    rcases onlf with _ | ⟨⟨lf⟩⟩  <;> simp [onlfvoc] at *
    · rcases x_in_V with ⟨f, f_in_fs, x_in⟩
      specialize unfvoc f_in_fs
      aesop
    · simp [pairUnload] at unfvoc
      rcases x_in_V with ⟨f, f_in_fs, x_in⟩|_ <;> aesop
  case dia' α φ notAtom =>
    have unfvoc := @unfoldDiamond_voc x α φ
    rw [← unfoldDiamondLoaded'_eq α φ] at unfvoc
    specialize @unfvoc (pairUnload (fs, onlf)) (by simp only [List.mem_map]; use (fs,onlf))
    rcases onlf with _ | ⟨⟨lf⟩⟩  <;> simp [onlfvoc] at *
    · rcases x_in_V with ⟨f, f_in_fs, x_in⟩
      specialize unfvoc f_in_fs
      aesop
    · simp [pairUnload] at unfvoc
      rcases x_in_V with ⟨f, f_in_fs, x_in⟩|_ <;> aesop

theorem localRule_does_not_increase_vocab_L {Cond B}
    (rule : LocalRule Cond B) :
    ∀ res ∈ B, res.left.fvoc ⊆ Cond.left.fvoc := by
  rcases Cond with ⟨Lcond, Rcond, Ocond⟩
  intro res res_in_B x x_in_res
  cases rule
  case oneSidedL ress orule B_def =>
    subst B_def
    simp at res_in_B
    rcases res_in_B with ⟨L, L_in, def_res⟩
    subst def_res
    simp at *
    rcases x_in_res with ⟨ψ, ψ_in, x_in_voc_ψ⟩
    cases orule
    case nCo => aesop
    case box α φ α_notAt => have := unfoldBox_voc L_in ψ_in x_in_voc_ψ; simp_all
    case dia => have := unfoldDiamond_voc L_in ψ_in x_in_voc_ψ; simp_all
    all_goals aesop
  case loadedL ress χ lrule B_def =>
    subst B_def
    simp at res_in_B
    rcases res_in_B with ⟨L, lnf, in_ress, def_res⟩
    subst def_res
    simp only [List.fvoc, Vocab.fromList, Sequent.left, Sequent.L, Sequent.O, List.map_append,
      List.toFinset_append, Finset.mem_sup, Finset.mem_union, List.mem_toFinset, List.mem_map,
      id_eq, List.empty_eq, List.nil_append, exists_exists_and_eq_and] at *
    rcases x_in_res with ⟨φvoc, ⟨φ, φ_in_L, def_φvoc⟩|⟨φ, φ_in_OlfL, def_φvoc⟩, x_in_φvoc⟩
    all_goals
      subst def_φvoc
      simp only [Olf.L, List.mem_cons, List.not_mem_nil, or_false, exists_eq_left, Formula.voc]
      have := LoadRule_voc lrule
      simp only [lfovoc, List.fvoc, Vocab.fromList, LoadFormula.voc] at this
      apply this; clear this
      simp [onlfvoc]
      refine ⟨L, lnf, in_ress, ?_⟩
    · left
      use φ
    · right
      cases lnf <;> simp [Olf.L] at *
      subst φ_in_OlfL
      exact x_in_φvoc
  -- other cases are all trivial (as in Bml)
  all_goals
    aesop

theorem localRule_does_not_increase_vocab_R (rule : LocalRule Cond B) :
    ∀ res ∈ B, res.right.fvoc ⊆ Cond.right.fvoc := by
  rcases Cond with ⟨Lcond, Rcond, Ocond⟩
  intro res res_in_B x x_in_res
  cases rule
  case oneSidedR ress orule B_def =>
    subst B_def
    simp at res_in_B
    rcases res_in_B with ⟨L, L_in, def_res⟩
    subst def_res
    simp at *
    rcases x_in_res with ⟨ψ, ψ_in, x_in_voc_ψ⟩
    cases orule
    case nCo => aesop
    case box α φ α_notAt => have := unfoldBox_voc L_in ψ_in x_in_voc_ψ; simp_all
    case dia => have := unfoldDiamond_voc L_in ψ_in x_in_voc_ψ; simp_all
    all_goals aesop
  case loadedR ress χ lrule B_def =>
    subst B_def
    simp at res_in_B
    rcases res_in_B with ⟨L, lnf, in_ress, def_res⟩
    subst def_res
    simp only [List.fvoc, Vocab.fromList, Sequent.right, Sequent.R, Sequent.O, List.map_append,
      List.toFinset_append, Finset.mem_sup, Finset.mem_union, List.mem_toFinset, List.mem_map,
      id_eq, List.empty_eq, List.nil_append, exists_exists_and_eq_and] at *
    rcases x_in_res with ⟨φvoc, ⟨φ, φ_in_L, def_φvoc⟩|⟨φ, φ_in_OlfR, def_φvoc⟩, x_in_φvoc⟩
    all_goals
      subst def_φvoc
      simp only [Olf.R, List.mem_cons, List.not_mem_nil, or_false, exists_eq_left, Formula.voc]
      have := LoadRule_voc lrule
      simp only [lfovoc, List.fvoc, Vocab.fromList, LoadFormula.voc] at this
      apply this; clear this
      simp [onlfvoc]
      refine ⟨L, lnf, in_ress, ?_⟩
    · left
      use φ
    · right
      cases lnf <;> simp [Olf.R] at *
      subst φ_in_OlfR
      exact x_in_φvoc
  -- other cases are all trivial (as in Bml)
  all_goals
    aesop

theorem localRuleApp_does_not_increase_jvoc (lra : LocalRuleApp) :
    ∀ Y ∈ lra.C, jvoc Y ⊆ jvoc lra.X := by
  match lra with
  | @LocalRuleApp.mk L R O Lcond Rcond Ocond ress lrule C hC preconditionProof =>
    subst hC
    rintro ⟨cL, cR, cO⟩ C_in
    simp [applyLocalRule] at C_in
    rcases C_in with ⟨⟨Lres, Rres, Ores⟩ , res_in, cLRO_def⟩
    simp at cLRO_def
    cases cLRO_def
    apply jvoc_sub_of_voc_sub -- hhmmm?
    · intro x x_in
      simp at * -- only
      rcases x_in with ⟨φvoc, ⟨φ, φ_nocon, d_φvoc⟩|⟨φ, φ_L, d_φvoc⟩|⟨φ, φ_O, d_φvoc⟩, x_in_φvoc⟩
      all_goals subst d_φvoc
      · refine ⟨φ.voc, Or.inl ?_, x_in_φvoc⟩
        exact ⟨φ, List.diff_subset L Lcond φ_nocon, rfl⟩
      all_goals
        have Lsub := @localRule_does_not_increase_vocab_L _ _ lrule _ res_in x
        simp at Lsub
      · specialize Lsub φ.voc (Or.inl ⟨_, φ_L, rfl⟩) x_in_φvoc
        rcases Lsub with ⟨ψvoc, h_ψvoc, x_in_ψvoc⟩
        refine ⟨ψvoc, ?_, x_in_ψvoc⟩
        rcases h_ψvoc with (⟨ψ, ψ_in_Lcond, d_ψvoc⟩ | ⟨ψ, ψ_in_Ocond, d_ψvoc⟩) <;> subst d_ψvoc
        · exact Or.inl ⟨ψ, preconditionProof.1.subset ψ_in_Lcond, rfl⟩
        · refine Or.inr ⟨ψ, ?_, rfl⟩; aesop
      · -- whether to use φ.voc depends on whether the localRuleApp changes the O here.
        rcases O with _|χ <;> rcases Ocond with _|cχ <;> rcases Ores with _|resχ
        all_goals
          simp [Olf.change] at * -- already treats 3 cases
        · specialize Lsub φ.voc (Or.inr ⟨φ, φ_O, rfl⟩) x_in_φvoc
          rcases Lsub with ⟨ψ, ψ_in_Lcond, x_in_φvoc⟩
          exact ⟨ψ, preconditionProof.1.subset ψ_in_Lcond, x_in_φvoc⟩
        · rcases χ with ⟨⟨χ⟩⟩|⟨χ⟩ <;> simp [Olf.L] at *
          subst φ_O
          aesop
        · rcases resχ with ⟨⟨resχ⟩⟩|⟨⟨resχ⟩⟩ <;> simp [Olf.L] at φ_O Lsub
          subst φ_O
          simp at x_in_φvoc
          specialize Lsub (resχ.unload).voc (Or.inr rfl) x_in_φvoc
          rcases Lsub with ⟨ψ, ψ_in_Lcond, x_in_φvoc⟩
          exact ⟨ψ.voc, Or.inl ⟨ψ, preconditionProof.1.subset ψ_in_Lcond, rfl⟩, x_in_φvoc⟩
        · rcases preconditionProof with ⟨Lin, Rin, same_form⟩
          subst same_form
          simp at φ_O
        · specialize Lsub φ.voc (Or.inr ⟨φ, φ_O, rfl⟩) x_in_φvoc
          rcases Lsub with ⟨ψvoc, ψ_defs, x_in_ψvoc⟩
          rcases ψ_defs with ⟨ψ, ψ_in, ψvoc_def⟩|⟨ψ, ψ_in, ψvoc_def⟩ <;> subst ψvoc_def
          · exact ⟨ψ.voc, Or.inl ⟨ψ, preconditionProof.1.subset ψ_in, rfl⟩, x_in_ψvoc⟩
          · refine ⟨ψ.voc, Or.inr ⟨ψ, ?_, rfl⟩, x_in_ψvoc⟩
            simp_all
    · -- analogous to L side, but quite some modifications
      intro x x_in
      simp at * -- only
      rcases x_in with ⟨φvoc, ⟨φ, φ_nocon, d_φvoc⟩|⟨φ, φ_L, d_φvoc⟩|⟨φ, φ_O, d_φvoc⟩, x_in_φvoc⟩
      all_goals subst d_φvoc
      · refine ⟨φ.voc, Or.inl ?_, x_in_φvoc⟩
        exact ⟨φ, List.diff_subset R Rcond φ_nocon, rfl⟩
      all_goals
        have Rsub := @localRule_does_not_increase_vocab_R _ _ lrule _ res_in x
        simp at Rsub
      · specialize Rsub φ.voc (Or.inl ⟨_, φ_L, rfl⟩) x_in_φvoc
        rcases Rsub with ⟨ψvoc, h_ψvoc, x_in_ψvoc⟩
        refine ⟨ψvoc, ?_, x_in_ψvoc⟩
        rcases h_ψvoc with (⟨ψ, ψ_in_Rcond, d_ψvoc⟩ | ⟨ψ, ψ_in_Ocond, d_ψvoc⟩) <;> subst d_ψvoc
        · exact Or.inl ⟨ψ, preconditionProof.2.1.subset ψ_in_Rcond, rfl⟩
        · refine Or.inr ⟨ψ, ?_, rfl⟩; aesop
      · -- whether to use φ.voc depends on whether the localRuleApp changes the O here.
        rcases O with _|χ <;> rcases Ocond with _|cχ <;> rcases Ores with _|resχ
        all_goals
          simp [Olf.change] at * -- already treats 3 cases
        · specialize Rsub φ.voc (Or.inr ⟨φ, φ_O, rfl⟩) x_in_φvoc
          rcases Rsub with ⟨ψ, ψ_in_Rcond, x_in_φvoc⟩
          exact ⟨ψ, preconditionProof.2.subset ψ_in_Rcond, x_in_φvoc⟩
        · rcases χ with ⟨⟨χ⟩⟩|⟨χ⟩ <;> simp [Olf.R] at *
          subst φ_O
          aesop
        · rcases resχ with ⟨⟨resχ⟩⟩|⟨⟨resχ⟩⟩ <;> simp [Olf.R] at φ_O Rsub
          subst φ_O
          simp at x_in_φvoc
          specialize Rsub (resχ.unload).voc (Or.inr rfl) x_in_φvoc
          rcases Rsub with ⟨ψ, ψ_in_Rcond, x_in_φvoc⟩
          exact ⟨ψ.voc, Or.inl ⟨ψ, preconditionProof.2.subset ψ_in_Rcond, rfl⟩, x_in_φvoc⟩
        · rcases preconditionProof with ⟨Lin, Rin, same_form⟩
          subst same_form
          simp at φ_O
        · specialize Rsub φ.voc (Or.inr ⟨φ, φ_O, rfl⟩) x_in_φvoc
          rcases Rsub with ⟨ψvoc, ψ_defs, x_in_ψvoc⟩
          rcases ψ_defs with ⟨ψ, ψ_in, ψvoc_def⟩|⟨ψ, ψ_in, ψvoc_def⟩ <;> subst ψvoc_def
          · refine ⟨ψ.voc, Or.inl ⟨ψ, preconditionProof.2.1.subset ψ_in, rfl⟩, x_in_ψvoc⟩
          · refine ⟨ψ.voc, Or.inr ⟨ψ, ?_, rfl⟩, x_in_ψvoc⟩
            simp_all

/-- Maehara's method for single-step *local* rule applications.
This covers easy cases without any loaded path repeats.
We do *not* use `localRuleTruth` to prove this,
but the more specific lemmas `oneSidedL_sat_down` and `oneSidedL_sat_down`. -/
def localInterpolantStep (lra : LocalRuleApp)
    (subθs : ∀ c ∈ lra.C, PartInterpolant c)
    : PartInterpolant lra.X := by
  -- UNPACKING TERMS
  rcases lra with ⟨L, R, o, Lcond, Rcond, Ocond, ress, rule, C, hC, precondProof⟩
  -- DISTINCTION ON LOCALRULE USED
  cases def_rule : rule
  case oneSidedL ress orule YS_def => -- rule applied in first component L
    let interList := C.attach.map <| fun c => (subθs c.1 c.2).1
    refine ⟨dis interList, ?_, ?_, ?_⟩ -- disjunction here
    · intro n n_in_inter
      rw [in_voc_dis] at n_in_inter
      rcases n_in_inter with ⟨φ, φ_in, n_in_voc_φ⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at φ_in
      rcases φ_in with ⟨Y, Y_in, def_φ⟩
      apply localRuleApp_does_not_increase_jvoc _ Y Y_in
      subst def_φ
      exact (subθs Y Y_in).prop.1 n_in_voc_φ
    · rintro nInter_L_sat
      have LI_sat : satisfiable (Sequent.left (L, R, o) ∪ (interList.map Formula.neg)) := by
        rcases nInter_L_sat with ⟨W, M, w, w_nInter_L⟩
        use W, M, w
        simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_cons, List.mem_append,
          forall_eq_or_imp, evaluate, disEval, not_exists, not_and] at w_nInter_L
        simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_union_iff, List.mem_append,
          List.mem_map]
        rintro φ (φ_in | ⟨θi,θi_in, φ_def⟩)
        · apply w_nInter_L.2; assumption
        · subst φ_def; apply w_nInter_L.1; assumption
      have := oneSidedL_sat_down ⟨L,R,o⟩ precondProof.1.subset orule YS_def LI_sat
      rcases this with ⟨⟨L', R', o'⟩, c_in, W, M, w, w_⟩
      refine (subθs ⟨L', R', o'⟩ (hC ▸ def_rule ▸ c_in)).2.2.1 ⟨W, M, w, ?_⟩ -- given IP property
      intro φ φ_in; simp at φ_in
      apply w_; simp [interList]; grind
    · rintro ⟨W, M, w, w_⟩
      simp only [Sequent.right, Sequent.R, Sequent.O, List.mem_cons, List.mem_append,
        forall_eq_or_imp, disEval, List.mem_map, List.mem_attach, true_and, Subtype.exists,
        interList] at w_
      rcases w_ with ⟨⟨θi, ⟨c, c_in, def_θi⟩, w_θi⟩, w_R⟩
      refine (subθs c (hC ▸ c_in)).2.2.2 ⟨W, M, w, ?_⟩ -- given IP property
      have same_R : c.right = Sequent.right (L,R,o) :=
        @oneSidedL_preserves_right (L,R,o) _ precondProof.1.subset _ orule _ YS_def c (hC ▸ c_in)
      rw [same_R]; simp; grind
  case oneSidedR ress orule YS_def => -- rule applied in second component R
    -- Only somewhat analogous to oneSidedR. Part 2 and 3 are flipped around in a way.
    let interList := C.attach.map <| fun c => (subθs c.1 c.2).1
    refine ⟨Con interList, ?_, ?_, ?_⟩ -- using conjunction here
    · intro n n_in_inter
      rw [in_voc_con] at n_in_inter
      rcases n_in_inter with ⟨φ, φ_in, n_in_voc_φ⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at φ_in
      rcases φ_in with ⟨Y, Y_in, def_φ⟩
      apply localRuleApp_does_not_increase_jvoc _ Y Y_in
      subst def_φ
      exact (subθs Y Y_in).prop.1 n_in_voc_φ
    · rintro ⟨W, M, w, w_⟩
      simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_cons, List.mem_append,
        forall_eq_or_imp, evaluate, conEval, not_forall] at w_
      rcases w_ with ⟨⟨θi, θi_in, w_nθi⟩, w_L⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at θi_in
      rcases θi_in with ⟨⟨L', R', o'⟩, c_in, def_θi⟩
      have same_L : Sequent.left ⟨L', R', o'⟩ = Sequent.left (L,R,o) :=
        @oneSidedR_preserves_left (L,R,o) _ precondProof.2.1.subset _ orule _ YS_def _ (hC ▸ c_in)
      refine (subθs ⟨L', R', o'⟩ (hC ▸ c_in)).2.2.1 ⟨W, M, w, ?_⟩
      rw [same_L]; simp; grind
    · rintro inter_R_sat
      have RI_sat : satisfiable (Sequent.right (L, R, o) ∪ (interList)) := by
        rcases inter_R_sat with ⟨W, M, w, w_Inter_R⟩
        use W, M, w; simp [conEval] at *; grind
      have := oneSidedR_sat_down ⟨L,R,o⟩ precondProof.2.1.subset orule YS_def RI_sat
      rcases this with ⟨⟨L', R', o'⟩, c_in, W, M, w, w_⟩
      refine (subθs ⟨L', R', o'⟩ (hC ▸ def_rule ▸ c_in)).2.2.2 ⟨W, M, w, ?_⟩ -- given IP property
      intro φ φ_in; simp at φ_in
      simp [interList, Sequent.right] at w_
      grind
  case LRnegL φ =>
    use φ
    simp only at subθs
    simp only [LocalRuleApp.X]
    refine ⟨?_, ?_, ?_⟩
    · intro n n_in_φ
      aesop
    · rintro ⟨W, M, w, w_⟩
      simp only [List.empty_eq, List.mem_cons, forall_eq_or_imp, evaluate] at *
      subst_eqs
      absurd w_.1
      have := w_.2 φ
      simp_all
    · rintro ⟨W, M, w, w_⟩
      simp only [List.empty_eq, List.mem_cons, forall_eq_or_imp] at *
      subst_eqs
      absurd w_.1
      have := w_.2 (~φ)
      simp_all [evaluate]
  case LRnegR φ =>
    use ~φ
    simp only at subθs
    simp only [LocalRuleApp.X]
    refine ⟨?_, ?_, ?_⟩
    · intro n n_in_φ
      aesop
    · rintro ⟨W, M, w, w_⟩
      simp only [List.empty_eq, List.mem_cons, forall_eq_or_imp, evaluate] at *
      subst_eqs
      absurd w_.1
      have := w_.2 (~φ)
      simp_all [evaluate]
    · rintro ⟨W, M, w, w_⟩
      simp only [List.empty_eq, List.mem_cons, forall_eq_or_imp, evaluate] at *
      subst_eqs
      absurd w_.1
      have := w_.2 φ
      simp_all
  case loadedL ress χ lrule YS_def =>
    -- similar to oneSidedL case
    simp at YS_def
    let interList := C.attach.map <| fun c => (subθs c.1 c.2).1
    have O_is_some : Sequent.O (L, R, o) = some (Sum.inl (~'χ)) := by
        have := precondProof.2.2; simp at this; simp; exact this.symm
    refine ⟨dis interList, ?_, ?_, ?_⟩ -- disjunction here
    · intro n n_in_inter
      rw [in_voc_dis] at n_in_inter
      rcases n_in_inter with ⟨φ, φ_in, n_in_voc_φ⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at φ_in
      rcases φ_in with ⟨Y, Y_in, def_φ⟩
      apply localRuleApp_does_not_increase_jvoc _ Y Y_in
      subst def_φ
      exact (subθs Y Y_in).prop.1 n_in_voc_φ
    · rintro nInter_L_sat
      have LI_sat : satisfiable (Sequent.left (L, R, o) ∪ (interList.map Formula.neg)) := by
        rcases nInter_L_sat with ⟨W, M, w, w_nInter_L⟩
        use W, M, w
        simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_cons, List.mem_append,
          forall_eq_or_imp, evaluate, disEval, not_exists, not_and] at w_nInter_L
        simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_union_iff, List.mem_append,
          List.mem_map]
        rintro φ (φ_in | ⟨θi,θi_in, φ_def⟩)
        · apply w_nInter_L.2; assumption
        · subst φ_def; apply w_nInter_L.1; assumption
      have := loadedL_sat_down ⟨L,R,o⟩ χ O_is_some lrule YS_def LI_sat
      rcases this with ⟨⟨L', R', o'⟩, c_in, W, M, w, w_⟩
      refine (subθs ⟨L', R', o'⟩ (hC ▸ def_rule ▸ c_in)).2.2.1 ⟨W, M, w, ?_⟩ -- given IP property
      intro φ φ_in; simp at φ_in
      apply w_; simp [interList]; grind
    · rintro ⟨W, M, w, w_⟩
      simp only [Sequent.right, Sequent.R, Sequent.O, List.mem_cons, List.mem_append,
        forall_eq_or_imp, disEval, List.mem_map, List.mem_attach, true_and, Subtype.exists,
        interList] at w_
      rcases w_ with ⟨⟨θi, ⟨c, c_in, def_θi⟩, w_θi⟩, w_R⟩
      refine (subθs c (hC ▸ c_in)).2.2.2 ⟨W, M, w, ?_⟩ -- given IP property
      have same_R : c.right = Sequent.right (L,R,o) :=
        @loadedL_preserves_right ⟨L,R,o⟩ χ O_is_some ress lrule _ YS_def c (hC ▸ c_in)
      rw [same_R]; simp; grind
  case loadedR ress χ lrule YS_def =>
    -- based on oneSidedR case
    let interList := C.attach.map <| fun c => (subθs c.1 c.2).1
    have O_is_some : Sequent.O (L, R, o) = some (Sum.inr (~'χ)) := by
      have := precondProof.2.2; simp at this; simp; exact this.symm
    refine ⟨Con interList, ?_, ?_, ?_⟩ -- using conjunction here
    · intro n n_in_inter
      rw [in_voc_con] at n_in_inter
      rcases n_in_inter with ⟨φ, φ_in, n_in_voc_φ⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at φ_in
      rcases φ_in with ⟨Y, Y_in, def_φ⟩
      apply localRuleApp_does_not_increase_jvoc _ Y Y_in
      subst def_φ
      exact (subθs Y Y_in).prop.1 n_in_voc_φ
    · rintro ⟨W, M, w, w_⟩
      simp only [Sequent.left, Sequent.L, Sequent.O, List.mem_cons, List.mem_append,
        forall_eq_or_imp, evaluate, conEval, not_forall] at w_
      rcases w_ with ⟨⟨θi, θi_in, w_nθi⟩, w_L⟩
      simp only [List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at θi_in
      rcases θi_in with ⟨⟨L', R', o'⟩, c_in, def_θi⟩
      have same_L : Sequent.left ⟨L', R', o'⟩ = Sequent.left (L,R,o) :=
        @loadedR_preserves_left (L,R,o) _ O_is_some _ lrule _ YS_def _ (hC ▸ c_in)
      refine (subθs ⟨L', R', o'⟩ (hC ▸ c_in)).2.2.1 ⟨W, M, w, ?_⟩
      rw [same_L]; simp; grind
    · rintro inter_R_sat
      have RI_sat : satisfiable (Sequent.right (L, R, o) ∪ (interList)) := by
        rcases inter_R_sat with ⟨W, M, w, w_Inter_R⟩
        use W, M, w; simp [conEval] at *; grind
      have := loadedR_sat_down ⟨L,R,o⟩ χ O_is_some lrule YS_def RI_sat
      rcases this with ⟨⟨L', R', o'⟩, c_in, W, M, w, w_⟩
      refine (subθs ⟨L', R', o'⟩ (hC ▸ def_rule ▸ c_in)).2.2.2 ⟨W, M, w, ?_⟩ -- given IP property
      intro φ φ_in; simp at φ_in
      simp only [Sequent.right, Sequent.R_eq, Sequent.O_eq, List.mem_union_iff, List.mem_append,
        List.mem_map, List.mem_attach, true_and, Subtype.exists, interList] at w_
      grind

/-! ## Interpolants for Local Tableau -/

def LocalTableau.interpolant (ltX : LocalTableau X)
    (endθs : ∀ Y ∈ endNodesOf ltX, PartInterpolant Y)
    : PartInterpolant X := by
  cases ltX
  case byLocalRule lra nexts X_def =>
    subst X_def
    apply localInterpolantStep lra
    intro Y Y_in
    have IH := LocalTableau.interpolant (nexts Y Y_in)
    exact IH (fun Z Z_in_end => endθs _ (endNodeOfChild_to_endNode lra nexts rfl Y_in Z_in_end))
  case sim Xbas =>
    apply endθs X
    simp [endNodesOf]

/-! ## Interpolants for PdlRules applied to free nodes

The only rule treated here is (L+), i.e. `loadL` and `loadR`.
-/

def freePdlRuleInterpolant {X Y} (r : PdlRule X Y) (Xfree : X.isFree) (θY : PartInterpolant Y)
    : PartInterpolant X := by
  rcases θY with ⟨θ, θ_ip_Y⟩
  cases r
  case loadL in_L notBox Y_def =>
    use θ
    subst Y_def
    rcases θ_ip_Y with ⟨hYvoc, hYL, hYR⟩
    refine ⟨?_, ?_, ?_⟩
    · intro x x_in
      specialize hYvoc x_in
      simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.left_eq, Olf.L_inl, unload_boxes,
        LoadFormula.unload, List.map_append, List.map_cons, Formula.voc, List.map_nil,
        List.toFinset_append, List.toFinset_cons, List.toFinset_nil, insert_empty_eq,
        Finset.union_singleton, Finset.sup_insert, id_eq, Finset.sup_eq_union', Sequent.right_eq,
        Olf.R_inl, List.append_nil, Finset.mem_inter, Finset.mem_union, Finset.mem_sup,
        List.mem_toFinset, List.mem_map, exists_exists_and_eq_and] at hYvoc
      simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.left_eq, Olf.L_none, List.append_nil,
        Sequent.right_eq, Olf.R_none, Finset.mem_inter, Finset.mem_sup, List.mem_toFinset,
        List.mem_map, id_eq, exists_exists_and_eq_and]
      rcases hYvoc with ⟨x_from, ⟨φ, φ_inR, x_from_φ⟩⟩
      constructor
      · rcases x_from with (hx|hx)
        · exact ⟨_, in_L, hx⟩
        · grind
      · use φ
    all_goals
      clear notBox Xfree
      simp at *
      grind
  case loadR in_R notBox Y_def=>
    use θ
    subst Y_def
    rcases θ_ip_Y with ⟨hYvoc, hYL, hYR⟩
    refine ⟨?_, ?_, ?_⟩
    · intro x x_in
      specialize hYvoc x_in
      simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.left_eq, Olf.L_inr, List.append_nil,
        Sequent.right_eq, Olf.R_inr, unload_boxes, LoadFormula.unload, List.map_append,
        List.map_cons, Formula.voc, List.map_nil, List.toFinset_append, List.toFinset_cons,
        List.toFinset_nil, insert_empty_eq, Finset.union_singleton, Finset.sup_insert, id_eq,
        Finset.sup_eq_union', Finset.mem_inter, Finset.mem_sup, List.mem_toFinset, List.mem_map,
        exists_exists_and_eq_and, Finset.mem_union] at hYvoc
      simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.left_eq, Olf.L_none, List.append_nil,
        Sequent.right_eq, Olf.R_none, Finset.mem_inter, Finset.mem_sup, List.mem_toFinset,
        List.mem_map, id_eq, exists_exists_and_eq_and]
      rcases hYvoc with ⟨⟨φ, φ_inR, x_from_φ⟩, x_from⟩
      constructor
      · use φ
      · rcases x_from with (hx|hx)
        · exact ⟨_, in_R, hx⟩
        · grind
    all_goals
      clear notBox Xfree
      simp at *
      grind
  all_goals
    exfalso
    subst_eqs

/-! ## Cluster tools -/

def repeatsOf {tab : Tableau .nil X} (s : PathIn tab) : List (PathIn tab) :=
  sorry

def all_cEdge {X} {tab : Tableau .nil X} (s : PathIn tab) : List (PathIn tab) :=
  sorry

lemma all_cEdge_spec {X} {tab : Tableau .nil X} (s t : PathIn tab) :
    t ∈ all_cEdge s ↔ ((s ⋖_ t) ∨ s ♥ t) := by
  sorry

def all_cEdge_rev {X} {tab : Tableau .nil X} (t : PathIn tab) : List (PathIn tab) :=
  sorry

lemma all_cEdge_rev_spec {X} {tab : Tableau .nil X} (s t : PathIn tab) :
    s ∈ all_cEdge_rev t ↔ ((s ⋖_ t) ∨ s ♥ t) := by
  sorry

/-- Loaded nodes "below" the given one, also allowing ♥ steps. -/
def loadedBelow : PathIn tab → List (PathIn tab) := sorry

/-- Loaded nodes "above" the given one, also allowing *backwards* ♥ steps. -/
def loadedAbove : PathIn tab → List (PathIn tab) := sorry

/-- List of all other nodes in the same cluster, essentially a constructive version of `clusterOf`.
Computed as the intersection of `loadedAbove` and `loadedBelow`. -/
def clusterListOf (p : PathIn tab) : List (PathIn tab) := loadedBelow p  ∩  loadedBelow p

lemma clusterListOf_spec (p : PathIn tab) :
    q ∈ clusterListOf p  ↔  p ≡ᶜ q := by
  sorry

-- TODO: how to convert `clusterListOf` result back to a tree?
-- Essentially, this would incorporate Lemma 7.22 (a).

def rootOf : List (PathIn tab) → PathIn tab := sorry -- just take the shortest?

-- Or would it be better to already construct (partial) trees instead of lists directly?

-- OR just assume are given the root of a cluster and use the tableau as it is to define `Q`?

/-- Lemma 7.22 (b) for left side -/
lemma cluster_all_left_loaded {p : PathIn tab} (h : (nodeAt p).2.2.isLeft) :
    ∀ q ∈ clusterListOf p, (nodeAt q).2.2.isLeft := by
  sorry

/-- Lemma 7.22 (b) for right side -/
lemma cluster_all_right_loaded {p : PathIn tab} (h : (nodeAt p).2.2.isRight) :
    ∀ q ∈ clusterListOf p, (nodeAt q).2.2.isRight := by
  sorry

def isExitIn : Sequent → List Sequent → Prop := sorry

instance : Decidable (isExitIn X C) := sorry

/-! ## Quasi-Tableaux -/

-- Alternative idea for def 7.31:
-- Instead of labelling nodes in Q with finite sequents, label them with the path to where
-- that sequent comes from in `Λ₂[C⁺]`?

inductive Typ | one | two | three -- lower case because these are not `Type`s.
open Typ

/-- Simple tree data type for `Q` in Def. 7.31. -/
inductive QuasiTab : Type | QNode : (k : Typ) → (Δ : Sequent) → (next : List QuasiTab) → QuasiTab
open QuasiTab

-- TODO add invariant?!

-- TODO use `rep` instead of `X ∈ Hist` maybe?

def Qchildren (C : List Sequent) : (k : Typ) → (Hist : List Sequent) → (X : Sequent) → List QuasiTab
| .one, Hist, X => -- case k(x)=1
    if X ∈ Hist ∨ isExitIn X C
      then [ ] -- then x is a leaf
      else [ QNode .two X (Qchildren C .two (X :: Hist) X) ]
| .two, Hist, X => -- case k(x)=2
    [ QNode .three X (Qchildren C .three (X :: Hist) X) ]
| .three, Hist, X => -- case k(x)=3
    if X.basic -- (Paper does "not basic" first.)
    then
      -- unique child with .one and result of PDL rule application
      -- PROBLEM: needs uniformity?
      sorry
    else
      -- create children based on local rule
      sorry
termination_by
  1 -- O.o ... see remark after Def 7.31 ;-)
decreasing_by
  · sorry
  · sorry

/-- Quasi-Tableau from Def 7.31. Here we "start the construction", then use `Qchildren`.
No names for the nodes as we use an inductive type, so we just write `X` for `Δₓ` -/
def Q {r : PathIn tab} : QuasiTab :=
  let X := nodeAt r -- FIXME wlog we only want the right sequent. But `.R` is not enogh !?!?!?!?!?
  QNode .one X (Qchildren ((clusterListOf r).map nodeAt) .one [] X)

/-! ## Interpolants for proper clusters -/

/-- Helper for `exitsOf`. Or replace it fully with this? -/
def exitsOf (tab : Tableau Hist X) : List (PathIn tab) :=
  if X.isLoaded
  then
    match tab with
    | .lrep _ => [] -- A repeat has no exit! (To ensure termination we do not rewind here.)
    | .loc _ _ lt next => (endNodesOf lt).attach.flatMap (fun ⟨Y,Y_in⟩ =>
                                  (exitsOf (next Y Y_in)).map (PathIn.loc Y_in))
    | .pdl _ _ _ next => (exitsOf next).map PathIn.pdl
  else
    [ .nil ] -- Free nodes are their own (and the only kind of exits.

lemma loaded_iff_exitsOf_non_nil {tab : Tableau Hist X} :
    X.isLoaded ↔ ∀ q ∈ exitsOf tab, q ≠ .nil := by
  constructor
  · intro Xload q q_in
    unfold exitsOf at q_in
    simp only [Xload, ↓reduceIte] at q_in
    cases tab <;> simp at q_in <;> grind
  · intro no_nil
    unfold exitsOf at no_nil
    grind

-- move to TableauPath.lean later
def PathIn.children : (p : PathIn tab) → List (PathIn tab) := sorry

/-- Specific version of `clusterInterpolation` where loaded formula is on the right side.
This may need additional hypotheses to say that we start at the root of the cluster. -/
def clusterInterpolation_right {Hist L R nlf}
    (tab : Tableau Hist (L, R, some (Sum.inr nlf)))
    (exitIPs : ∀ e ∈ exitsOf tab, PartInterpolant (nodeAt e))
    : PartInterpolant (L, R, some (Sum.inr nlf)) := by
  sorry

/-- TODO messy helper for `mem_existsOf_of_flip` -/
lemma PathIn.loc_flip
    nrep nbas
    (lt : LocalTableau (L, R, some (Sum.inl nlf)))
    (next : (Y : _) → Y ∈ endNodesOf lt → Tableau ((L, R, some (Sum.inl nlf)) :: Hist) Y)
    (next' : (Yf : _) → Yf ∈ endNodesOf lt.flip → Tableau ((_ :: Hist).map Sequent.flip) Yf)
    (Y : Sequent)
    (Y_in : Y ∈ endNodesOf lt)
    (p : PathIn (next Y Y_in))
    (Yf_in : Y.flip ∈ endNodesOf lt.flip)
    (h_next : (next Y Y_in).flip = next' Y.flip Yf_in)
    (h : (Tableau.loc nrep nbas lt.flip next') = (Tableau.loc nrep' nbas' lt next).flip)
    :
    (PathIn.loc Y_in nil).flip = h ▸ @PathIn.loc _ _ _ _ lt.flip next' _ Yf_in .nil := by
  -- cases h -- oho!
  simp [PathIn.flip] -- hmm
  sorry

/-! The following lemma about `PathIn.flip` is here because it is also about `exitsOf`. -/

lemma mem_existsOf_of_flip {Hist L R lr_nlf} {tab : Tableau Hist (L, R, some lr_nlf)}
    (s : PathIn tab.flip) (s_in : s ∈ (exitsOf tab.flip : List (PathIn tab.flip)))
    : (PathIn_type_flip_flip ▸ s.flip) ∈ exitsOf tab := by
  unfold exitsOf at *
  rcases lr_nlf with (nlf|nlf)
  · cases tab
    case loc nbas lt nrep next =>
      simp only [Sequent.flip, Olf.flip, Option.map_some, Sum.swap_inl, Tableau.flip,
        Sequent.isLoaded, decide_true, ↓reduceIte, List.mem_flatMap, List.mem_attach, List.mem_map,
        true_and, Subtype.exists, Sum.swap_inr] at *
      rcases s_in with ⟨Yf, Yf_in, e, e_in, def_s⟩
      rcases exists_flip_of_endNodesOf Yf_in with ⟨Y, def_Yf, Y_in⟩
      subst def_Yf def_s
      rcases Y with ⟨LY, RY, (_|lr_nlf_Y)⟩
      · simp [Sequent.flip] at e
        simp_all [Sequent.flip]
        rw! (castMode := .all) [Olf.flip_none] at e_in; simp at e_in
        rw [LocalTableau.flip_flip] at Y_in
        -- Should this be exfalso here?
        -- - Show that Y must be loaded -- should follow from what here?
        -- Or is it okay for an exit to be "inside" / right after a local tableau?
        refine ⟨ (LY,RY,none), Y_in, .nil, ?_, ?_⟩
        · unfold exitsOf
          simp [Sequent.isLoaded]
        · unfold exitsOf at e_in
          simp [Sequent.isLoaded] at e_in
          subst e_in
          unfold PathIn.flip; simp
          unfold PathIn.flip; simp
          -- TODO: Lemma about .loc .nil and cast?
          sorry
      · rcases lr_nlf_Y with (nlfY|nlfY)
        · sorry -- analogous later?
        simp [Sequent.flip] at *
        unfold Sequent.flip at e e_in
        simp at e e_in
        unfold Olf.flip at e e_in
        simp at e e_in
        have := endNodesOf_flip Yf_in
        simp only [Sequent.flip_flip] at this
        refine ⟨(LY, RY, some _), this, ?_⟩
        use (PathIn_type_flip_flip ▸ e.flip)
        constructor
        · have IH := mem_existsOf_of_flip e e_in -- yeah?!
          grind
        · -- only HEq business left to do here?
          sorry
    case pdl Y bas r nrep next =>
      simp_all only [Sequent.flip, Olf.flip, Option.map_some, Sum.swap_inl, Tableau.flip,
        Sequent.isLoaded, decide_true, ↓reduceIte, List.mem_map, Sum.swap_inr]
      rcases s_in with ⟨e, e_in, def_s⟩
      subst def_s
      rcases Y with ⟨LY, RY, (_|lr_nlf_Y)⟩ -- to get that Y is loaded
      · unfold exitsOf at ⊢ e_in
        simp [Sequent.isLoaded] at *
        subst e_in
        -- only HEq business left to do here?
        sorry
      · have IH := mem_existsOf_of_flip e e_in
        use (PathIn_type_flip_flip ▸ e.flip), IH
        simp
        -- only HEq business left to do here?
        sorry

    case lrep =>
      simp [Sequent.isLoaded, Sequent.flip, Olf.flip] at *
      simp_all [Tableau.flip]

  · -- other `nlf` side, should be analogous.
    sorry
termination_by
  s.length
decreasing_by
  · simp_wf
    subst_eqs
    sorry
  · -- use that length goes down with `edge`?
    sorry

def exitsOf_flip {tab : Tableau Hist (L, R, some nlf)}
    (exitIPs : ∀ e ∈ exitsOf tab, PartInterpolant (nodeAt e)) :
    ∀ e ∈ exitsOf tab.flip, PartInterpolant (nodeAt e) := by
  intro e e_in
  specialize exitIPs _ (mem_existsOf_of_flip _ e_in)
  have : (nodeAt (PathIn_type_flip_flip ▸ e.flip)) = (nodeAt e).flip := by
    convert PathIn.nodeAt_flip <;> simp_all [Sequent.flip, Olf.flip]
  rw [this] at exitIPs
  rcases exitIPs with ⟨θ, ⟨hVoc, hL, hR⟩⟩
  refine ⟨~θ, ?_, ?_, ?_⟩
  · intro x x_in
    specialize hVoc x_in
    simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.flip_left, Sequent.flip_right,
      Finset.mem_inter, Finset.mem_sup, List.mem_toFinset, List.mem_map, id_eq,
      exists_exists_and_eq_and] at hVoc
    aesop
  · simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.flip_left, Sequent.flip_right, listHasSat,
    List.mem_cons, forall_eq_or_imp, evaluate, not_exists, not_and, not_forall, not_not] at *
    intro W M w w_θ
    exact hR W M w w_θ
  · simp only [jvoc, List.fvoc, Vocab.fromList, Sequent.flip_left, Sequent.flip_right, listHasSat,
    List.mem_cons, forall_eq_or_imp, evaluate, not_exists, not_and, not_forall] at *
    intro W M w not_w_θ
    exact hL W M w not_w_θ

/-- When `X` is an interpolant for `X`, then `~θ` is an interpolant for `X.flip`. -/
lemma IsPartInterpolant.flip : isPartInterpolant X θ → isPartInterpolant X.flip (~θ) := by
  rintro ⟨voc, l_ip, r_ip⟩
  refine ⟨?_, ?_, ?_⟩
  · clear l_ip r_ip
    simp_all
    intro x x_in
    specialize voc x_in
    simp_all
  · simp_all
  · simp_all

/-- Given a tableau where the root is loaded, and exit interpolants, interpolate the root. -/
def clusterInterpolation {Hist L R snlf}
    (tab : Tableau Hist (L, R, some snlf))
    (exitIPs : ∀ e ∈ exitsOf tab, PartInterpolant (nodeAt e))
    : PartInterpolant (L, R, some snlf) := by
  cases snlf
  case inl nlf =>
    -- We "flip" the left/right sides of `tab` so we can apply the wlog version.
    let f_tab := tab.flip
    let f_exitIPs : ∀ e ∈ exitsOf tab.flip, PartInterpolant (nodeAt e) := exitsOf_flip exitIPs
    have := @clusterInterpolation_right _ _ _ nlf f_tab f_exitIPs
    rcases this with ⟨θ, isInter⟩
    refine ⟨~θ, ?_⟩
    apply IsPartInterpolant.flip isInter
  case inr nlf =>
    exact @clusterInterpolation_right _ _ _ nlf tab exitIPs

-- MOVE AWAY after proving
lemma PathIn.lt_append_iff_not_eq {tab : Tableau Hist X}
  (p : PathIn tab) (h : tabAt p = ⟨pHist, ⟨pX, tabNew⟩⟩) (q : PathIn tabNew)
  : p < p.append (h ▸ q) ↔ p ≠ p.append (h ▸ q) := by
  have := @PathIn.eq_append_iff_other_eq_nil _ _ tab p (h ▸ q)
  cases q_def : q
  case nil => -- showing false ↔ false
    have : p.append (h ▸ .nil) = p := by grind
    simp_all
    exact path_is_irreflexive
  case loc => -- showing true ↔ true
    have : p.append (h ▸ q) ≠ p := by
      subst q_def
      simp
      rw [Eq.comm, PathIn.eq_append_iff_other_eq_nil]
      intro hyp
      -- ???
      sorry
    simp [q_def] at this
    simp only [ne_eq]
    rw [Eq.comm]
    simp only [this, not_false_eq_true, iff_true, gt_iff_lt]
    clear this
    sorry
  case pdl => -- showing true ↔ true
    simp_all
    sorry

/-- Ideally this would be a computable `def` and not an existential.
But currently `PathIn.strong_upwards_inductionOn` only works with `Prop` motive. -/
theorem tabToIntAt {X : Sequent} (tab : Tableau .nil X) (s : PathIn tab) :
    ∃ θ, isPartInterpolant (nodeAt s) θ := by
  induction s using PathIn.strong_upwards_inductionOn -- Strong!
  next s IH =>
  -- case distinction before or after `induction`?
  by_cases (nodeAt s).isLoaded
  case pos s_loaded =>
    -- HARD case, here we want to use `clusterInterpolation` and that is why we used
    -- `PathIn.strong_upwards_inductionOn` to have an IH applicable to "far away" exits.
    -- WORRY: the IH here is not morally true, might later need to restrict it to free nodes.
    -- Now we use `s_loaded` to get the right input type for `clusterInterpolation`.
    rcases tab_s_def : tabAt s with ⟨Hist, ⟨L,R, olf⟩, tabNew⟩
    cases olf
    case none =>
      exfalso
      unfold nodeAt at s_loaded
      rw [tab_s_def] at s_loaded
      simp [Sequent.isLoaded] at s_loaded
    case some lr_nlf =>
      let myExitIPs : ∀ e ∈ exitsOf tabNew, PartInterpolant (nodeAt e) := by
        intro e e_in
        specialize @IH (s.append (tab_s_def ▸ e)) ?_
        · -- Use that `exitsOf` something loaded are proper successors.
          have e_non_nil : e ≠ .nil := by
            unfold nodeAt at s_loaded
            have := (@loaded_iff_exitsOf_non_nil _ _ (tabAt s).2.2).mp s_loaded (tab_s_def ▸ e) ?_
            · convert this <;> grind
            grind
          have := PathIn.lt_append_iff_not_eq _ tab_s_def e
          simp at this
          grind
        have := IH.choose_spec
        simp only [nodeAt_append] at this IH
        refine ⟨IH.choose, ?_⟩
        convert this <;> try rw [tab_s_def]
        rw! [tab_s_def]; simp
      have := @clusterInterpolation _ L R lr_nlf tabNew myExitIPs
      rcases this with ⟨θ, h_θ⟩
      use θ
      unfold nodeAt
      rw [tab_s_def]
      simp
      exact h_θ
  case neg s_free =>
    -- EASY case, singleton cluster because not loaded.
    simp at s_free
    rcases s_def : tabAt s with ⟨Hist, X, s_tab⟩
    cases s_tab_def : s_tab
    case loc nbas ltX nrep nexts =>
      /- -- Interestingly, we do not care about the end node being free here.
      have Xfree : X.isFree := by rw [nodeAt, s_def] at s_free; grind [Sequent.isFree]
      have endFree := fun Y => @endNodesOf_free_are_free _ Y ltX Xfree
      -/
      have endIPsExist : ∀ Y ∈ endNodesOf ltX, ∃ θ, isPartInterpolant Y θ := by
        intro Y Y_in
        subst s_tab_def -- hmm?
        -- Need to make a path-step to Y, def and proofs about it inspired by `Soundness.lean`
        let s_to_u : PathIn (tabAt s).2.2 := s_def ▸ @PathIn.loc _ _ nrep nbas ltX nexts Y Y_in .nil
        let u := s.append s_to_u
        have s_u : s ⋖_ u := by
          unfold u s_to_u
          apply edge_append_loc_nil
          grind
        specialize IH (Relation.TransGen.single s_u)
        have tabAt_u_def : tabAt u = ⟨_, ⟨Y, nexts Y Y_in⟩⟩ := by
          unfold u s_to_u
          rw [tabAt_append]
          have : (tabAt (PathIn.loc Y_in PathIn.nil : PathIn (Tableau.loc nrep nbas ltX nexts)))
              = ⟨X :: _, ⟨Y, nexts Y Y_in⟩⟩ := by simp_all
          convert this <;> try rw [s_def]
          rw [eqRec_heq_iff_heq]
        unfold nodeAt at IH
        rw [tabAt_u_def] at IH
        exact IH
      let ltIP := LocalTableau.interpolant ltX ?endNodeIPs
      · rcases ltIP with ⟨θ, X_ip_θ⟩
        use θ
        unfold nodeAt
        rw [s_def]
        simp_all
      · intro Y Y_in
        specialize endIPsExist Y Y_in
        exact ⟨endIPsExist.choose, endIPsExist.choose_spec⟩
    case pdl Y bas r nrep next =>
      subst s_tab_def
      -- The def of `t` here is inspired by the proof of `tableauThenNotSat` (with s/t swapped).
      let s_to_t : PathIn (Tableau.pdl nrep bas r next) := (.pdl .nil)
      let t : PathIn tab := s.append (s_def ▸ s_to_t)
      have s_t : s ⋖_ t := by
          convert @edge_append_pdl_nil .nil _ tab s (s_def ▸ nrep)
                                        (s_def ▸ bas) Y (s_def ▸ r) (s_def ▸ next) ?_ <;> grind
      have def_Y : nodeAt t = Y := by
        simp only [t, s_to_t, nodeAt_append]
        convert @nodeAt_pdl_nil _ _ _ nrep bas next r <;> grind
      specialize IH (Relation.TransGen.single s_t)
      unfold nodeAt at s_free
      rw [s_def] at s_free
      simp only at s_free
      unfold nodeAt
      rw [s_def]
      simp only
      rw [def_Y] at IH
      rcases IH with ⟨θY, θY_ip_Y⟩
      have := freePdlRuleInterpolant r (by grind [Sequent.isFree]) ⟨θY, θY_ip_Y⟩
      rcases this with ⟨θX, θX_ipX⟩
      use θX
    case lrep lpr =>
      exfalso
      absurd s_free
      rw [nodeAt, s_def]
      simp only [Bool.not_eq_false]
      apply LoadedPathRepeat_rep_isLoaded lpr

theorem tabToInt {X : Sequent} (tab : Tableau .nil X) :
    ∃ θ, isPartInterpolant X θ := tabToIntAt tab .nil
